import core.io;
import std.conv;
import std.socket;
import core.string;

enum : int
| Error : -1
| Success : 0
| WrongArgs : 1
 -> RetCode;

imut ExitCmd = "exit";

def main(args) -> int
{
    let retCode = RetCode::Success;
    let port = 8888us;
    let serverIp = "127.0.0.1".dup();

    if (args.len > 1)
    {
        if (args.len != 3)
        {
            printUsage();
            return RetCode::WrongArgs;
        }

        
        serverIp = args[1];
        port = to!ushort (args[2]);
    }
    
    let sock = socket (AddrFamily::AfInet, SocketType::SockStream, 0);
    if (sock < 0)
    {
        println ("Socket error");
        return RetCode::Error;
    }
    else
    {
        let serverAddr = SockAddrIn::init;
        serverAddr.sin_addr.s_addr = inet_addr (serverIp.ptr);
        serverAddr.sin_family = cast!short (AddrFamily::AfInet);
        serverAddr.sin_port = htons (port);

        if (connect (sock, cast!(p!SockAddr) (&serverAddr), SockAddrIn::sizeof) < 0)
        {
            println ("Connect error");
            retCode = RetCode::Error;
        }
        else
        {
            println ("Connected to server !");

            while (handleCmd(sock)) {}
        }
    }

    close (sock);
    return retCode;
}

def printUsage()
{
    println("Usage :");
    println(" client [ip_address port]");
    println("\n Without any ip address and port, the default values are 127.0.0.1 and 8888.\n");
}

def handleCmd(sock : int) -> bool
{
    print (" > ");
    let cmd = scanString();

    if (cmd == ExitCmd)
        return false;

    let messageSize = cast!uint(cmd.len);
    let ret = send (sock, cast!(p!void) (&messageSize), uint::sizeof, 0);
    if (ret < 0)
    {
        println ("Send size failed !");
    }
    else
    {
        print ("Sending a message of : "); println (messageSize);
        ret = send (sock, cast!(p!void) (cmd.ptr), messageSize, 0);
        if (ret < 0)
            println ("Send message failed");
        else
            print ("Sent a message of "); println (ret);
    }
    
    return true;
}
