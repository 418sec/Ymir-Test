mod src.screen;

extern (C) printf (const c : p!char, ...);

import core.io;
import std.abort;
import std.list;
import src.lib.sdl;

struct
| window : p!SDL_Window
| renderer : p!SDL_Renderer
| rects : List!(p!SDL_Rect)
| background : SDL_Rect
 -> Screen;

private
{
    def printErr (const message : string)
    {
        print (message, " : ");
        printf ("%s".ptr, SDL_GetError());
    }
}

// TODO : the createwindow flag should come from the main function in order to propose the fullscreen mode
def createScreen (const title : string, const windowWidth : int, const windowHeight : int, const windowPosX : int, const windowPosY : int) -> Screen
{
    if (SDL_Init (SDL_InitFlag::Video) != 0)
    {
        printErr ("SDL_Init() error");
        abort ();
    }

    let screen = Screen::init;
    screen.window = SDL_CreateWindow (title.ptr, windowPosX, windowPosY, windowWidth, windowHeight, SDL_WindowFlag::Shown);

    if (screen.window is null)
    {
        printErr ("SDL_CreateWindow() error");
        abort ();
    }

    // -1 : init the first rendering driver supporting the requested flag (index of rendering driver) 
    screen.renderer = SDL_CreateRenderer (screen.window, -1, SDL_RendererFlag::Accelerated);

    if (screen.renderer is null)
    {
        printErr ("SDL_CreateRenderer() error");
        abort ();
    }

    SDL_RenderSetLogicalSize (screen.renderer, windowWidth, windowHeight);

    screen.rects = list!(p!SDL_Rect) ();
    
    return screen;
}

def clear (ref screen : Screen)
{
    SDL_SetRenderDrawColor (screen.renderer, 0ub, 0ub, 0ub, 255ub);
    SDL_RenderClear (screen.renderer);
}

def display (ref screen : Screen)
{
    SDL_SetRenderDrawColor (screen.renderer, 255ub, 255ub, 255ub, 255ub);

    let next_ = screen.rects.next ();
    while (next_ !is null)
    {
        SDL_RenderFillRect (screen.renderer, (*next_).value);
        next_ = (*next_).next;
    }
    
    SDL_RenderPresent (screen.renderer);
}

def destroy (ref screen : Screen)
{
    println ("destroy !");
    if (screen.renderer !is null)
        SDL_DestroyRenderer (screen.renderer);
    if (screen.window !is null)
        SDL_DestroyWindow (screen.window);
}

def addRect (ref screen : Screen, rect : p!SDL_Rect)
{
    screen.rects.push (rect);
}
