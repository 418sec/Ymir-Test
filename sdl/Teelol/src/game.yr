mod src.game;

import core.io;
import core.string;
import std.memory;
import src.lib.signal;
import src.lib.sdl;
import src.screen;
import src.eventHandler;
import src.player;

struct
| screen : Screen
| eventHdl : EventHandler
| continue : bool
| mainPlayer : Player
 -> Game;

// for now...
imut WindowWidth = 600;
imut WindowHeight = 300;
imut WindowPosX = 100;
imut WindowPosY = 100;
imut Title = "Teelol";
imut PlayerName = "Abitbol";

def createGame (ref game : Game) 
{
    game.screen = createScreen (Title, WindowWidth, WindowHeight, WindowPosX, WindowPosY);
    game.eventHdl = createEventHandler (game);
    game.continue = true;
    game.mainPlayer = createPlayer (PlayerName.dup(), 100, 100);

    game.screen.addRect (&(game.mainPlayer.rect));
    
    initEvents (game.eventHdl);
}

def start (ref game : Game)
{
    let screen = game.screen;
    // we don't forget to free sdl at the end of the scope !
    on exit => screen.destroy();
    
    while (game.continue)
    {
        screen.clear();
        game.eventHdl.handleEvents ();
        screen.display();
    }
}

def initEvents (ref eventHdl : EventHandler)
{
    let quitSignal = Signal!(ref Game)::init;
    quitSignal.connect (onQuit);
    let keyUpSignal = Signal!(ref Game)::init;
    keyUpSignal.connect (onKeyUp);
    let keyDownSignal = Signal!(ref Game)::init;
    keyDownSignal.connect (onKeyDown);
    let keyRightSignal = Signal!(ref Game)::init;
    keyRightSignal.connect (onKeyRight);
    let keyLeftSignal = Signal!(ref Game)::init;
    keyLeftSignal.connect (onKeyLeft);
    
    eventHdl.onEvent (SDL_EventType::Quit, quitSignal);
    eventHdl.onEvent (SDL_EventType::KeyUp, SDL_Keycode::SDLK_ESCAPE, quitSignal);
    eventHdl.onEvent (SDL_EventType::KeyDown, SDL_Scancode::SDL_SCANCODE_UP, keyUpSignal);
    eventHdl.onEvent (SDL_EventType::KeyDown, SDL_Scancode::SDL_SCANCODE_DOWN, keyDownSignal);
    eventHdl.onEvent (SDL_EventType::KeyDown, SDL_Scancode::SDL_SCANCODE_RIGHT, keyRightSignal);
    eventHdl.onEvent (SDL_EventType::KeyDown, SDL_Scancode::SDL_SCANCODE_LEFT, keyLeftSignal);
}

def onQuit (ref game : Game)
{
    game.continue = false;
    println ("Bye !");
}

def onKeyUp (ref game : Game)
{
    game.mainPlayer.rect.y -= 10;
}

def onKeyDown (ref game : Game)
{
    game.mainPlayer.rect.y += 10;
}

def onKeyRight (ref game : Game)
{
    game.mainPlayer.rect.x += 10;
}

def onKeyLeft (ref game : Game)
{
    game.mainPlayer.rect.x -= 10;
}
